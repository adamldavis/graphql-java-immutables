/*
 * This Spock specification was generated by the Gradle 'init' task.
 */
package com.adamldavis.gji

import com.adamldavis.gji.processing.Element
import com.adamldavis.gji.processing.SchemaScriptBase
import org.codehaus.groovy.control.CompilerConfiguration
import spock.lang.Specification

class AppTest extends Specification {

    def "should convert to groovy"() {
        when:
        def result = App.toGroovy('type Groovy { names: [String!]! }')

        then:
        result == 'x0.type.Groovy { names% String%1+1%1 }\nprocess_graph_root'
    }

    def "should create type elements"() {
        given:
        def groovy = 'type.Groovy { names% String%1+1 }'
        def compilerConfiguration = new CompilerConfiguration()
        compilerConfiguration.setScriptBaseClass(SchemaScriptBase.class.name)
        GroovyShell shell = new GroovyShell(compilerConfiguration)
        when:
        Element element = shell.evaluate(groovy)
        then:
        element.children.size() == 1
        element.value == 'type'
        element.attributes == [new Element.Attribute('Groovy')]
        element.children.first().value == 'names'
        element.children.first().attributes == [new Element.Attribute('String'),
                new Element.Attribute(Element.NON_NULL), new Element.Attribute(Element.ARRAY)]
    }

    def "should create input elements"() {
        given:
        def groovy = 'input.Groovy { version% Version%1 \n num% Int }'
        def compilerConfiguration = new CompilerConfiguration()
        compilerConfiguration.setScriptBaseClass(SchemaScriptBase.class.name)
        GroovyShell shell = new GroovyShell(compilerConfiguration)
        when:
        Element element = shell.evaluate(groovy)
        then:
        element.children.size() == 2
        element.value == 'input'
        element.attributes == [new Element.Attribute('Groovy')]
        element.children.first().value == 'version'
        element.children.first().attributes == [new Element.Attribute('Version'),
                                                new Element.Attribute(Element.NON_NULL)]
        element.children.last().value == 'num'
        element.children.last().attributes.last() == new Element.Attribute('Int')
    }
}
