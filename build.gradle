
plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'groovy'
    id 'application'
}

repositories {
    jcenter()
}
configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.codehaus.groovy:groovy'

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'io.cucumber:cucumber-java:5.6.0'
}

application {
    // Define the main class for the application
    mainClassName = 'com.adamldavis.gji.App'
}

test {
    useJUnitPlatform()
}
group = 'com.adamldavis'
version = '0.0.1'

tasks.build.dependsOn = ['test', 'bootJar']

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        def result = javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'cucumber', 'src/test/resources', '--strict']
        }
        result.assertNormalExitValue()
    }
}
